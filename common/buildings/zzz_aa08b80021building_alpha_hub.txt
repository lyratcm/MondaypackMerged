building_alpha_hub = {
    base_buildtime = @b3_time
    base_cap_amount = 1
    icon = building_autocurating_vault
    category = unity
    potential = {
        exists = owner
        building_basic_income_check = yes
        owner = {
            is_machine_empire = yes
        }
    }
    allow = {
        has_fully_upgraded_capital = yes
    }
    destroy_trigger = {
        exists = owner
        OR = {
            owner = {
                is_machine_empire = no
            }
            buildings_no_crystals = yes
        }
    }
    convert_to = {
        building_autocurating_vault
        building_citadel_of_faith
        building_corporate_vault
        building_hive_primary_confluence
    }
    resources = {
        category = planet_buildings
        cost = {
            minerals = @b2_minerals
            rare_crystals = @b2_rare_cost
        }
        upkeep = {
            energy = @b2_upkeep
            rare_crystals = @b2_rare_upkeep
        }
        produces = {
            unity = 10
        }
    }
    planet_modifier = {
        planet_evaluators_unity_produces_mult = 0.15
    }
    prerequisites = {
        "tech_autocurating_vault"
    }
    show_tech_unlock_if = {
        is_machine_empire = yes
    }
    ai_weight = {
        weight = 0
        modifier = {
            weight = 1
            buildings_has_crystals = yes
        }
        modifier = {
            factor = 0
            has_global_flag = gai_enabled_flag
            owner = {
                has_country_flag = buildings_new_crystal_building
            }
        }
    }
    on_queued = {
        IF = {
            limit = {
                owner = {
                    has_technology = tech_construction_templates
                }
            }
            owner = {
                set_timed_country_flag = {
                    flag = buildings_new_crystal_building
                    days = 270
                }
            }
        }
        ELSE_IF = {
            limit = {
                owner = {
                    has_technology = tech_assembly_pattern
                }
            }
            owner = {
                set_timed_country_flag = {
                    flag = buildings_new_crystal_building
                    days = 390
                }
            }
        }
        else = {
            planet = {
                owner = {
                    set_timed_country_flag = {
                        flag = buildings_new_crystal_building
                        days = 510
                    }
                }
            }
        }
    }
}
