apothecary = {
    category = criminal
    building_icon = building_chemical_plant
    clothes_texture_index = 2
    icon = chemist
    possible_pre_triggers = {
        has_owner = yes
        is_being_purged = no
        is_being_assimilated = no
    }
    possible = {
        hidden_trigger = {
            exists = owner
        }
        exists = owner
        NOR = {
            has_citizenship_type = {
                country = owner
                type = citizenship_robot_servitude
            }
            has_trait = trait_hive_mind
            has_trait = trait_machine_unit
            AND = {
                owner = {
                    has_valid_civic = civic_machine_assimilator
                }
                has_trait = trait_cybernetic
            }
        }
    }
    planet_modifier = {
        planet_crime_add = 1
        planet_amenities_add = 2
        planet_jobs_produces_mult = -0.01
    }
    triggered_planet_modifier = {
        potential = {
            OR = {
                has_trait = trait_pheremones
                has_trait = trait_lithoid_shiny
            }
        }
        modifier = {
            planet_amenities_add = 0.8
        }
    }
    triggered_planet_modifier = {
        potential = {
            OR = {
                has_trait = trait_robot_domestic_protocols
                has_trait = trait_charismatic
                has_trait = trait_robot_emotion_emulators
            }
        }
        modifier = {
            planet_amenities_add = 0.4
        }
    }
    triggered_planet_modifier = {
        potential = {
            has_global_flag = expanded_pops_active
            is_robot_pop = yes
            exists = owner
            owner = {
                has_relic = r_three_londor
            }
        }
        modifier = {
            planet_amenities_add = 0.4
        }
    }
    triggered_planet_modifier = {
        potential = {
            OR = {
                has_trait = trait_repugnant
                has_trait = trait_robot_uncanny
            }
        }
        modifier = {
            planet_amenities_add = -0.4
        }
    }
    resources = {
        category = planet_artisans
        produces = {
            consumer_goods = 2
        }
    }
    weight = {
        weight = @criminal_job_weight
    }
}
