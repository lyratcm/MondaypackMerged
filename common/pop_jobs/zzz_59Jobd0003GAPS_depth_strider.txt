GAPS_depth_strider = {
    category = specialist
    condition_string = SPECIALIST_JOB_TRIGGER
    building_icon = building_power_plant
    clothes_texture_index = 2
    possible = {
        hidden_trigger = {
            exists = owner
        }
        specialist_job_check_trigger = yes
        owner = {
            is_gestalt = no
        }
    }
    possible_pre_triggers = {
        has_owner = yes
        is_being_purged = no
        is_being_assimilated = no
        battle_thrall_job_check_trigger = yes
    }
    resources = {
        category = planet_soldiers
        produces = {
            trigger = {
                exists = owner
                owner = {
                    has_valid_civic = civic_citizen_service
                }
            }
            unity = 2
        }
        produces = {
            trigger = {
                exists = owner
                owner = {
                    has_valid_civic = civic_police_state
                }
            }
            unity = 1
        }
        upkeep = {
            trigger = {
                exists = planet
                planet = {
                    has_modifier = anticrime_campaign
                }
            }
            energy = 2
        }
        upkeep = {
            sr_GAPS_water = 10
        }
    }
    pop_modifier = {
        pop_defense_armies_add = 2
    }
    planet_modifier = {
        planet_crime_add = -25
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                has_non_swapped_tradition = tr_domination_judgment_corps
            }
        }
        modifier = {
            planet_crime_add = -5
        }
    }
    triggered_planet_modifier = {
        potential = {
            exists = planet
            planet = {
                has_modifier = anticrime_campaign
            }
        }
        modifier = {
            planet_crime_add = -10
        }
    }
    country_modifier = {
        country_naval_cap_add = 4
    }
    triggered_country_modifier = {
        potential = {
            exists = planet
            planet = {
                exists = owner
                owner = {
                    has_technology = "tech_ground_defense_planning"
                }
            }
        }
        modifier = {
            country_naval_cap_add = 2
        }
    }
    pop_modifier = {
        pop_defense_armies_add = 3
    }
    triggered_planet_modifier = {
        potential = {
            exists = planet
            planet = {
                has_modifier = martial_law
            }
        }
        modifier = {
            planet_stability_add = 5
        }
    }
    weight = {
        weight = @specialist_job_weight
        modifier = {
            factor = 10
            exists = owner
        }
    }
}
