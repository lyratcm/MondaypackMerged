war_philosophy = {
    potential = {
        OR = {
            is_country_type = default
            is_country_type = fallen_empire
            is_country_type = awakened_fallen_empire
            is_country_type = ascended_empire
            is_country_type = lost_empire
        }
    }
    allow = {
        is_at_war = no
    }
    option = {
        name = "unrestricted_wars"
        policy_flags = {
            unrestricted_wars
        }
        in_breach_of = {
             {
                key = resolution_rulesofwar_last_resort_doctrine
            }
        }
        valid = {
            NOR = {
                is_subject_type = scion_signatory
                has_ethic = "ethic_pacifist"
                has_ethic = "ethic_fanatic_pacifist"
                has_valid_civic = "civic_inwards_perfection"
                has_valid_civic = "civic_closed_collective"
                has_valid_civic = "civic_closed_network"
                has_valid_civic = "civic_harm_hive"
                has_valid_civic = "civic_harm_machine"
                has_ai_personality = harmonious_hierarchy
            }
        }
        AI_weight = {
            modifier = {
                factor = 10
                OR = {
                    has_valid_ai_personality = no
                    is_ai = no
                }
                NOT = {
                    has_ethic = "ethic_pacifist"
                    has_ethic = "ethic_fanatic_pacifist"
                }
            }
            modifier = {
                factor = 10
                has_valid_ai_personality = yes
                is_ai = yes
                OR = {
                    has_ai_personality_behaviour = conqueror
                    has_ai_personality_behaviour = subjugator
                    has_ai_personality_behaviour = purger
                }
            }
            modifier = {
                factor = 0.1
                has_valid_ai_personality = yes
                is_ai = yes
                is_galactic_community_member = yes
                OR = {
                    is_active_resolution = "resolution_rulesofwar_last_resort_doctrine"
                    is_active_resolution = "resolution_rulesofwar_demobilization_initiative"
                }
            }
            modifier = {
                factor = 0
                OR = {
                    has_ethic = "ethic_pacifist"
                    has_ethic = "ethic_fanatic_pacifist"
                    has_ai_personality = open_hive_mind
                    has_ai_personality = open_machine_intelligence
                }
            }
        }
    }
    option = {
        name = "liberation_wars"
        policy_flags = {
            liberation_wars
        }
        potential = {
            NOT = {
                has_ethic = ethic_gestalt_consciousness
            }
        }
        valid = {
            NOR = {
                has_ethic = "ethic_fanatic_pacifist"
                has_valid_civic = "civic_fanatic_purifiers"
            }
        }
        on_enabled = {
            hidden_effect = {
                check_casus_belli_valid = {
                    type = cb_ideology
                }
            }
        }
        on_disabled = {
            hidden_effect = {
                check_casus_belli_valid = {
                    type = cb_ideology
                }
            }
        }
        AI_weight = {
            modifier = {
                factor = 2
                OR = {
                    has_valid_ai_personality = no
                    is_ai = no
                }
                has_ethic = "ethic_pacifist"
            }
            modifier = {
                factor = 2
                has_valid_ai_personality = yes
                is_ai = yes
                OR = {
                    has_ai_personality_behaviour = conqueror
                    has_ai_personality_behaviour = subjugator
                    has_ai_personality_behaviour = liberator
                }
            }
            modifier = {
                factor = 3
                has_valid_ai_personality = yes
                is_ai = yes
                OR = {
                    has_ai_personality_behaviour = conqueror
                    has_ai_personality_behaviour = subjugator
                    has_ai_personality_behaviour = liberator
                }
                has_authority = auth_democratic
                NOR = {
                    has_ethic = ethic_militarist
                    has_ethic = ethic_fanatic_militarist
                    has_ethic = ethic_xenophobe
                    has_ethic = ethic_fanatic_xenophobe
                }
            }
            modifier = {
                factor = 5
                has_valid_ai_personality = yes
                is_ai = yes
                has_ai_personality = democratic_crusaders
            }
            modifier = {
                factor = 5
                has_valid_ai_personality = no
                is_ai = yes
                has_ethic = ethic_fanatic_egalitarian
                has_ethic = ethic_militarist
            }
        }
    }
    option = {
        name = "no_wars"
        policy_flags = {
            no_wars
        }
        valid = {
            NOR = {
                has_ethic = "ethic_fanatic_militarist"
                has_valid_civic = "civic_fanatic_purifiers"
                has_valid_civic = "civic_hive_devouring_swarm"
                has_valid_civic = "civic_machine_terminator"
            }
        }
        AI_weight = {
            modifier = {
                factor = 5
                has_ethic = "ethic_fanatic_pacifist"
            }
            modifier = {
                factor = 1.5
                has_ethic = ethic_fanatic_xenophile
                is_authoritarian = no
                is_militarist = no
            }
            modifier = {
                factor = 1.5
                has_ethic = "ethic_pacifist"
            }
            modifier = {
                factor = 1.5
                OR = {
                    has_valid_civic = civic_diplomatic_corps
                    has_valid_civic = civic_public_relations_specialists
                    has_valid_civic = civic_hive_empath
                    has_valid_civic = civic_machine_emotions
                }
            }
            modifier = {
                factor = 0
                NOT = {
                    has_ethic = "ethic_fanatic_pacifist"
                }
            modifier = {
                factor = 0
                NOR = {
                    is_pacifist = yes
                    AND = {
                        has_ethic = ethic_fanatic_xenophile
                        is_authoritarian = no
                        is_militarist = no
                    }
                    has_ai_personality = open_hive_mind
                    has_ai_personality = open_machine_intelligence
                }
            }
        }
    }
}
