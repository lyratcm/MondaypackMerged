citizenship_limited = {
    token = citizenship_limited
    new_pop_weight = 0.75
    pop_modifier = {
        pop_happiness = -0.1
        pop_amenities_usage_mult = -0.25
        pop_political_power = -0.5
    }
    potential = {
        exists = from
        from = {
            NOR = {
                has_authority = auth_hive_mind
                has_authority = auth_machine_intelligence
                has_valid_civic = civic_hive_devouring_swarm
                has_valid_civic = civic_closed_collective
                has_valid_civic = civic_infestation
                has_valid_civic = civic_living_hosts
                has_valid_civic = civic_machine_assimilator
                has_valid_civic = civic_machine_servitor
                has_valid_civic = civic_machine_terminator
                has_valid_civic = civic_closed_network
            }
        }
        NOR = {
            has_trait = trait_machine_unit
            is_shackled_robot_species_rights = yes
        }
    }
    allow = {
        custom_tooltip = {
            fail_text = "HIVE_MIND_MAIN_SPECIES_ALWAYS_CITIZEN"
            NAND = {
                from = {
                    has_authority = auth_hive_mind
                }
                has_trait = trait_hive_mind
            }
        }
        custom_tooltip = {
            fail_text = "HIVE_MIND_MAIN_SPECIES_ALWAYS_CITIZEN"
            NAND = {
                from = {
                    has_authority = auth_machine_intelligence
                }
                OR = {
                    has_trait = trait_machine_unit
                    has_trait = trait_mechanical
                }
            }
        }
        custom_tooltip = {
            fail_text = same_species_disallowed
            OR = {
                NOT = {
                    is_same_species = from
                }
                AND = {
                    is_subspecies = from
                    from = {
                        OR = {
                            has_ethic = ethic_xenophobe
                            has_ethic = ethic_fanatic_xenophobe
                            has_ethic = ethic_authoritarian
                            has_ethic = ethic_fanatic_authoritarian
                        }
                        NOR = {
                            has_ethic = ethic_xenophile
                            has_ethic = ethic_fanatic_xenophile
                        }
                    }
                }
            }
        }
        custom_tooltip = {
            fail_text = "lonely_hive_mind_pops_kill_themselves_citizenship"
            is_lonely_hive_mind_pop = no
        }
        custom_tooltip = {
            fail_text = "disconnected_machine_pops_kill_themselves_citizenship"
            is_disconnected_machine_pop = no
        }
        custom_tooltip = {
            fail_text = "FANATIC_PURIFIERS_MUST_PURIFY"
            OR = {
                NOT = {
                    from = {
                        has_valid_civic = civic_fanatic_purifiers
                    }
                }
                is_same_species = from
            }
        }
        custom_tooltip = {
            fail_text = yes_service_yes_citizenship
            OR = {
                NOT = {
                    from = {
                        has_valid_civic = civic_citizen_service
                    }
                }
                NOT = {
                    has_military_service_type = {
                        type = military_service_full
                        country = from
                    }
                }
            }
        }
        custom_tooltip = {
            fail_text = no_mistreating_friends
            NOT = {
                from = {
                    has_valid_civic = civic_befrienders
                }
            }
        }
    }
    ai_will_do = {
        factor = 3
        modifier = {
            factor = 0
            is_subspecies = from
        }
        modifier = {
            factor = 0
            from = {
                has_valid_ai_personality = yes
                has_ai_personality_behaviour = multispecies
                is_gestalt = no
            }
        }
        modifier = {
            factor = 0
            from = {
                has_valid_ai_personality = yes
                NOT = {
                    has_ai_personality_behaviour = multispecies
                }
                is_gestalt = yes
            }
        }
        modifier = {
            factor = 0
            OR = {
                has_trait = trait_syncretic_proles
                has_trait = trait_nerve_stapled
                has_trait = trait_presapient_docile_livestock
            }
            from = {
                has_valid_ai_personality = yes
                has_ai_personality_behaviour = multispecies
                is_gestalt = yes
            }
        }
        modifier = {
            factor = 0
            from = {
                has_valid_ai_personality = no
                OR = {
                    has_ethic = ethic_xenophile
                    has_ethic = ethic_fanatic_xenophile
                }
            }
        }
    }
}
